apply plugin: 'eclipse'

buildscript {
	repositories {
	    mavenCentral()
	}

	dependencies {
		classpath 'com.bmuschko:gradle-docker-plugin:2.6'
	}
}

def dockerHost = "https://localhost:2376"
if (null != System.getenv("DOCKER_HOST"))
{
	dockerHost = System.getenv('DOCKER_HOST')

	if (dockerHost.indexOf('tcp') == 0)
	{
		dockerHost = dockerHost.replace('tcp','https')
	}
}

def dockerCertPath = ""
if (null != System.getenv('DOCKER_CERT_PATH')){
	dockerCertPath = System.getenv('DOCKER_CERT_PATH')
}

def determinedEnvironment = ["LICENSE=accept",
			"REGISTRATION_SECRET=" + System.getenv('REGISTRATION_SECRET'),
			"ADMIN_PASSWORD=" + System.getenv('ADMIN_PASSWORD'),
			"QUERY_SECRET=" + System.getenv('QUERY_SECRET')]

if (null != System.getenv("LOGSTASH_ENDPOINT"))
{
	determinedEnvironment << "LOGSTASH_ENDPOINT=" + System.getenv("LOGSTASH_ENDPOINT")
	determinedEnvironment << "LOGSTASH_KEY=" + System.getenv("LOGSTASH_KEY")
	determinedEnvironment << "LOGSTASH_CERT=" + System.getenv("LOGSTASH_CERT")
}


task copyTask(type: Copy) {
    from '../concierge-app/build/libs/concierge-app-1.0.war'
    into 'servers/gameon-concierge/apps/'
    rename("concierge-app-1.0.war", "concierge-app.war")
}

task build(dependsOn: ['copyTask']){

}

apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*

docker {
	  url = dockerHost
    certPath = new File(dockerCertPath)
}

task buildDockerImage(type: DockerBuildImage, dependsOn: 'copyTask') {
	inputDir = file('.')
    tag = 'gameon-concierge'
    quiet = false
}

task stopCurrentContainer(type: DockerStopContainer) {
    targetContainerId { "gameon-concierge" }
    timeout 0
}

task removeCurrentContainer(type: DockerRemoveContainer) {
	targetContainerId { "gameon-concierge" }
}

task createNewContainer(type: DockerCreateContainer) {
	targetImageId { "gameon-concierge" }
	containerName = "gameon-concierge"
	portBindings = ['9081:9081']
	env = determinedEnvironment
}

task startNewContainer(type: DockerStartContainer) {
	dependsOn createNewContainer
	targetContainerId { "gameon-concierge" }
}
