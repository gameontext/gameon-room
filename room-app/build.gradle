apply plugin: 'war'
apply plugin: 'eclipse-wtp'

sourceCompatibility = 1.7

dependencies {
    providedCompile group:'javax.websocket', name:'javax.websocket-api', version:'1.1'
    providedCompile group:'javax.ws.rs', name:'javax.ws.rs-api', version:'2.0'
    providedCompile group:'javax.json', name:'javax.json-api', version:'1.0'
    providedCompile group:'javax.servlet', name:'javax.servlet-api', version:'3.1.0'
    providedCompile 'javax.annotation:javax.annotation-api:1.2'
    providedCompile 'javax.inject:javax.inject:1'
    providedCompile 'javax.enterprise:cdi-api:1.2'
    compile 'org.mongodb:mongo-java-driver:2.12.3'
    compile project(':app-common')
}

// Set the Eclipse facets to use 3.1 of the Dynamic Web Module which requires Java 1.7 by default.
// Also include the JAX-RS and javascript
eclipse.wtp.facet {
    // Clear default facets to work around bug where you get duplicates including wst 2.4
    facets = []
    facet name: 'jst.java', type: 'fixed'
    facet name: 'jst.web', type: 'fixed'
    facet name: 'jst.web', version: '3.1'
    facet name: 'jst.java', version: '1.7'
    facet name: 'jst.jaxrs', version: '2.0'
    facet name: 'wst.jsdt.web', type: 'fixed'
    facet name: 'wst.jsdt.web', version: '1.0'
}


// Copy WAR into local server folder.
task publishWar(dependsOn: 'jar', type: Copy) {
    from(war)
    into('../room-wlpcfg/servers/gameon-room/apps')
}

assemble.dependsOn('publishWar')
